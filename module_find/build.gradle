if (isNeedFindModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        //ARouter的配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        //根据模块集成，动态加载配置文件
        main {
            if (isNeedFindModule.toBoolean()) {
                manifest.srcFile 'src/main/module_manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/app_manifest/AndroidManifest.xml'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //依赖公共模块
    implementation project(':module_common_base')

    //公共模块集成
    annotationProcessor "com.alibaba:arouter-compiler:1.2.1"
    implementation 'com.android.support:multidex:1.0.3'
}
